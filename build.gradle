plugins {
  id 'java'
  id 'groovy'
  id 'org.springframework.boot' version '2.3.10.RELEASE'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id "org.openapi.generator" version "5.1.1"
  id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.bzb.homebrew'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

ext {
  set('springCloudVersion', "Hoxton.SR11")
}

dependencies {

  implementation 'org.projectlombok:lombok:1.18.18'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  implementation "org.mapstruct:mapstruct:1.4.2.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"

  implementation 'io.swagger:swagger-annotations:1.6.2'
  implementation 'io.springfox:springfox-swagger2:2.9.2'
  implementation 'org.zalando:problem-spring-web-starter:0.26.2'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

  // Spock testing
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.codehaus.groovy:groovy:3.0.7'
  testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.spockframework:spock-spring:2.0-M4-groovy-3.0'

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

testSets {
  integrationTest {
    dirName = 'integration-test'
  }
}

check.dependsOn integrationTest

openApiGenerate {
  generatorName = "spring"
  inputSpec = "$rootDir/src/main/resources/contract/brewery-api.yaml".toString()
  outputDir = "$buildDir/generated/sources/openapi".toString()
  modelPackage = "com.bzb.homebrew.generated.openapi.model"
  apiPackage = "com.bzb.homebrew.generated.openapi.api"
  configOptions = [
      interfaceOnly       : "true",
      dateLibrary         : "java8",
      useBeanValidation   : "false",
      library             : "spring-cloud",
      openApiNullable     : "false",
      skipDefaultInterface: "true"
  ]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
  main {
    java.srcDirs += "build/generated/sources/openapi/src/main/java"
  }
}

tasks.withType(Test) {
  // Use junit platform for unit and integration tests.
  useJUnitPlatform()
}
